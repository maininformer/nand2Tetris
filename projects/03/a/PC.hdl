// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
     Inc16(in=previousout, out=incremented);
     
     // must always increment first so if there are 
     // multiple 1 flags it does not load and then add
     // or reset and then add, but rather add and then
     // load or reset

     Mux16(a=previousout, b=incremented, sel=inc, out=incin);
     Mux16(a=incin, b=in[0..15], sel=load, out=incloadin);
     Mux16(a=incloadin, b=false, sel=reset, out=newin);
     Or(a=reset, b=load, out=resetload);
     Or(a=inc, b=resetload, out=acceptin);
     Register(in=newin, load=acceptin, out=previousout, out=out[0..15]);

}
